apply plugin: 'groovy'
apply plugin: 'antlr'

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.5.3"
    compile "org.codehaus.groovy:groovy-all:2.4.7"
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

generateGrammarSource {
    arguments += ['-visitor', '-package', 'org.blinkenlights.basic.gen']
}

jar {
    manifest {
        attributes(
            'Main-Class': 'org.blinkenlights.basic.BasicInterpreterApp',
        )
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = 'all'
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

artifacts {
    archives fatJar
}
